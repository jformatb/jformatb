plugins {
    id 'eclipse'
    id 'org.sonarqube' version '3.3'
    id 'io.freefair.lombok' version '6.6.3' apply false
}

allprojects {

    group = 'jformatb'
    version = '0.1-SNAPSHOT'

    repositories {
        mavenCentral()
    }

}

subprojects {
    apply plugin: 'maven-publish'

    plugins.withType(JavaPlugin) {
        project.sourceCompatibility = '1.8'
        project.targetCompatibility = '1.8'
    }

    tasks.withType(JavaCompile) { task ->
        task.options.encoding = 'UTF-8'
        task.options.release = 8
    }

    publishing {
        publications {
            maven(MavenPublication) {
                pom {
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'yannickebongue'
                            name = 'Yannick Ebongue'
                            email = 'yannickebongue@gmail.com'
                        }
                    }
                }

                versionMapping {
                    usage('java-api') {
                        fromResolutionOf('runtimeClasspath')
                    }
                    usage('java-runtime') {
                        fromResolutionResult()
                    }
                }
            }
        }
    }
}
