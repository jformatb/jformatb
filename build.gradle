plugins {
    id 'eclipse'
    id 'jacoco-report-aggregation'
    id 'org.sonarqube' version '7.0.1.6134'
    id 'io.freefair.lombok' version '6.6.3' apply false
    id 'net.researchgate.release' version '3.1.0'
}

dependencies {
    jacocoAggregation project(':jformatb-api')
    jacocoAggregation project(':jformatb-ri')
}

release {
    git {
        requireBranch.set('main')
    }
}

allprojects {

    repositories {
        mavenLocal()
        mavenCentral()
    }

}

subprojects {
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    plugins.withType(JavaPlugin) {
        project.sourceCompatibility = '1.8'
        project.targetCompatibility = '1.8'
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
        options.release = 8
    }

    tasks.withType(Test) {
        systemProperty 'file.encoding', 'UTF-8'
    }

    tasks.withType(Sign) {
        onlyIf { !version.endsWith('SNAPSHOT') }
    }

    publishing {
        publications {
            maven(MavenPublication) {
                pom {
                    url = 'https://github.com/jformatb/jformatb'

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'yannickebongue'
                            name = 'Yannick Ebongue'
                            email = 'yannickebongue@gmail.com'
                        }
                    }

                    scm {
                        url = 'https://github.com/jformatb/jformatb'
                        connection = 'scm:git:git@github.com:jformatb/jformatb.git'
                        developerConnection = 'scm:git:git@github.com:jformatb/jformatb.git'
                    }
                }
            }
        }

        repositories {
            maven {
                def releaseRepoUrl = 'https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/'
                def snapshotRepoUrl = 'https://central.sonatype.com/repository/maven-snapshots/'

                name = 'MavenCentral'
                url = version.endsWith('SNAPSHOT') ? snapshotRepoUrl : releaseRepoUrl

                credentials {
                    username = findProperty('ossrhUsername') ?: System.getenv('OSSRH_USERNAME')
                    password = findProperty('ossrhPassword') ?: System.getenv('OSSRH_PASSWORD')
                }
            }

            maven {
                name = 'GithubPackages'
                url = 'https://maven.pkg.github.com/jformatb/jformatb'

                credentials {
                    username = findProperty('gpr.user') ?: System.getenv('USERNAME')
                    password = findProperty('gpr.key') ?: System.getenv('TOKEN')
                }
            }
        }
    }

    signing {
        def signingKey = findProperty("signingKey")
        def signingPassword = findProperty("signingPassword")
        useInMemoryPgpKeys(signingKey, signingPassword)
        sign publishing.publications.maven
    }
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testSuiteName = "test"
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

sonar {
    properties {
        property 'sonar.projectKey', 'jformatb_jformatb'
        property 'sonar.organization', 'jformatb'
        property 'sonar.host.url', 'https://sonarcloud.io'
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml'
    }
}
