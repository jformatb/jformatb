plugins {
    id 'java-platform'
    id 'jacoco-report-aggregation'
}

javaPlatform {
    allowDependencies()
}

dependencies {
    constraints {
        api project(':jformatb-api')
        api project(':jformatb-ri')
    }

    jacocoAggregation project(':jformatb-api')
    jacocoAggregation project(':jformatb-ri')
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.javaPlatform

            pom {
                name = 'Java Format Binding Platform'
                description = 'Bill of Material for getting full, complete set of compatible versions of Java Format Binding Platform'
            }
        }
    }
}

reporting {
    reports {
        testCodeCoverageReport(JacocoCoverageReport) {
            testType = TestSuiteType.UNIT_TEST
        }
    }
}

tasks.named('check') {
    dependsOn tasks.named('testCodeCoverageReport', JacocoReport)
}

sonar {
    properties {
        property 'sonar.coverage.jacoco.xmlReportPaths', 'build/reports/jacoco/testCodeCoverageReport/testCodeCoverageReport.xml'
    }
}
