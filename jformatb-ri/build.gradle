plugins {
    id 'java-library'
    id 'jacoco'
    id 'io.freefair.lombok'
}

dependencies {
    api project(':jformatb-api')

    implementation platform(project(':jformatb-dependencies'))

    implementation 'commons-beanutils:commons-beanutils'
    implementation 'commons-codec:commons-codec'
    implementation 'org.apache.commons:commons-lang3'

    testImplementation 'org.assertj:assertj-core'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.platform:junit-platform-launcher'
    testImplementation 'org.hibernate.validator:hibernate-validator'
    testImplementation 'commons-validator:commons-validator'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind'
    testImplementation 'com.opencsv:opencsv:5.9'

    testRuntimeOnly 'org.glassfish:jakarta.el:3.0.3'
}

java {
    withJavadocJar()
    withSourcesJar()
}

tasks.named('jar') {
    manifest {
        attributes(
            'Implementation-Title': project.name,
            'Implementation-Version': project.version
        )
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport
}

jacocoTestReport {
    dependsOn test
    reports {
        csv.required = false
        xml.required = true
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java

            pom {
                name = 'Java Format Binding Runtime Implementation'
                description = 'An implementation of Java Format Binding API'
            }

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}
